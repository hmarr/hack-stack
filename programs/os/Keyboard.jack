// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key;
        let key = 0;
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }
        do Output.printChar(key);

        while (Keyboard.keyPressed() = key) {
        }

        return key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String line;
        var char c;
        var boolean done;

        do Output.printString(message);

        let line = String.new(64);
        while (~done) {
            let c = Keyboard.readChar();

            // Add printable characters to the string
            if ((c > 31) & (c < 127) & (line.length() < 64)) {
                do line.appendChar(c);
            }

            // Handle backspace
            if ((c = 129) & (line.length() > 0)) {
                do line.eraseLastChar();
            }

            // Stop when we get a return key
            if (c = 128) {
                let done = true;
            }
        }

        return line;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String line;
        var int n;

        let line = Keyboard.readLine(message);
        let n = line.intValue();
        do line.dispose();
        return n;
    }
}
